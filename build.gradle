apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
        classpath 'io.ratpack:ratpack-gradle:1.0.0'
        classpath 'com.intershop.gradle.dependencyanalysis:dependencyanalysis-gradle-plugin:1.0.1'
    }
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
    jacoco {
        toolVersion = '0.7.1.201405082137'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'sonar-runner'
    apply plugin: 'jacoco'
    apply plugin: 'osgi'
    apply plugin: 'com.intershop.gradle.dependencyanalysis'

    group = 'de.hatoka.offlinepoker'
    version = '0.1.15'
    status = 'development'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyAnalysis {
        failOnErrors = false
    }
    sonarRunner {
        sonarProperties {
            property "sonar.dynamicAnalysis", "reuseReports"
            property "sonar.host.url" , sonarURL
            property "sonar.projectName", "OfflinePoker " + name
            property "sonar.projectKey", "offlinepoker:${name}"
            property "sonar.projectVersion", "0.1"
            property "sonar.java.source", "8"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.jacoco.reportPath", "$buildDir/jacoco/jacocoTest.exec"
        }
    }

    jacocoTestReport {
        reports {
            csv.enabled false
            xml.enabled true
            html.enabled true
            html.destination "${buildDir}/jacoco/html"
        }
    }
    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        }
     }

    jacoco {
        reportsDir = file("$buildDir/jacoco/report")
    }

    jar {
        manifest {
            // the manifest of the default jar is of type OsgiManifest
            name = project.name
            instruction 'Bundle-Description', project.name
            instruction 'Bundle-Vendor', 'Thomas Bergmann'
        }
    }
}

project('de.hatoka.user')
{
    sourceSets {
        main {
          output.resourcesDir = "build/classes/main"
        }
    }

    dependencies {
        compile project(":de.hatoka.common")
        compile project(":de.hatoka.mail")
        runtime 'javax.mail:mail:1.4.7'

        testCompile project(":de.hatoka.test")
    }
}

project('de.hatoka.test')
{
    dependencies {
        compile project(":de.hatoka.common")
        compile 'org.eclipse.persistence:javax.persistence:2.1.1'
        compile 'junit:junit:4.12'
        compile 'org.apache.derby:derby:10.12.1.1'
        compile 'com.google.inject:guice:4.0'
        compile 'xmlunit:xmlunit:1.6'
        compile 'org.mockito:mockito-core:1.10.19'
        runtime 'xalan:xalan:2.7.2'
        runtime 'org.eclipse.persistence:eclipselink:2.6.3'
   }
}

project('de.hatoka.common')
{
    dependencies {
        compile 'com.google.inject:guice:4.0'
        compile 'commons-io:commons-io:2.+' // IOUtils
        compile 'commons-lang:commons-lang:2.+' // RandomUtils
        compile 'javax.servlet:javax.servlet-api:3.1.0' // HttpServletRequest
        compile 'javax.validation:validation-api:1.1.0.Final'
        compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
        compile 'org.eclipse.persistence:javax.persistence:2.1.1'
        compile 'org.slf4j:slf4j-api:1.7.21'

        testCompile 'junit:junit:4.12'
        testRuntime 'xalan:xalan:2.7.2'
    }
}

project('de.hatoka.mail')
{
    dependencies {
        compile project(":de.hatoka.common")
        compile 'javax.mail:mail:1.4.7'
        testCompile project(":de.hatoka.test")
    }
    test {
        // https://javamail.java.net/nonav/docs/api/com/sun/mail/smtp/package-summary.html
        // systemProperty 'SMTP_HOST', mailSmtpHost
        // systemProperty 'SMTP_USER', mailSmtpUser
        // systemProperty 'SMTP_PASSWORD', mailSmtpPassword
        // explicitly include or exclude tests
        exclude 'de/hatoka/mail/internal/service/**'
    }
}
project('de.hatoka.tournament')
{
    sourceSets {
        main {
          output.resourcesDir = "build/classes/main"
        }
    }

    dependencies {
        compile project(":de.hatoka.common")
        testCompile project(":de.hatoka.test")
    }
}

project('de.hatoka.group')
{
    dependencies {
        compile project(":de.hatoka.common")
        testCompile project(":de.hatoka.test")
    }
}

project('de.hatoka.offlinepoker')
{
    apply plugin: 'war'
    apply plugin: 'io.ratpack.ratpack-groovy'

    mainClassName = 'de.hatoka.offlinepoker.internal.app.OfflinePokerMain'
    dependencies {
        compile project(":de.hatoka.tournament")
        compile project(":de.hatoka.user")
        compile project(":de.hatoka.group")

        compile 'org.eclipse.jetty:jetty-server:9.3.5.v20151012'
        compile 'org.eclipse.jetty:jetty-servlet:9.3.5.v20151012'
        compile 'org.eclipse.jetty:jetty-webapp:9.3.5.v20151012'

        compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.3'
        compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
        compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.7.3'
        compile ('org.glassfish.jersey.containers:jersey-container-servlet:2.22.2')
        {
            exclude group: 'org.glassfish.hk2.external'
        }

        runtime 'org.apache.commons:commons-dbcp2:2.0.1'
        runtime 'org.postgresql:postgresql:9.3-1102-jdbc4'
        runtime 'xalan:xalan:2.7.2'
        runtime 'org.eclipse.persistence:eclipselink:2.6.3'

        testCompile 'junit:junit:4.12'
    }
    task copyWebApp(type: Copy) {
        from 'src/main/webapp'
        into 'build/resources/main'
    }
    task stage {
      dependsOn installDist
      dependsOn copyWebApp
    }
}

// based on http://www.jroller.com/aalmiray/entry/gradle_glam_jacoco_coveralls
task jacocoReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(fileTree('.').include('**/*.exec').getFiles())
    reports {
        csv.enabled = false
        html.enabled = true
        html.destination "${buildDir}/reports/jacoco/html"
        xml.enabled = true
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

defaultTasks 'test', 'war', 'jacocoReport'

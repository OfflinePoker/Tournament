apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
    }
}

ext {
    // Logging
    slf4jVersion = '1.7.9'
    logbackVersion = '1.1.2'

    // Testing
    junitVersion = '4.12'
    mockitoVersion = '1.9.5'
    apacheDerbyVersion = '10.11.1.1'
    jacocoVersion = '0.7.1.201405082137'

    // libraries
    jerseyVersion ='2.14'
    hibernateVersion='4.3.7.Final'
    tomcatVersion='8.0.20'
    mailVersion='1.4.7'

    bundleVendor = 'Thomas Bergmann'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'jacoco'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    jacoco {
        toolVersion = jacocoVersion
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'sonar-runner'
    apply plugin: 'jacoco'
    apply plugin: 'osgi'

    group = 'de.hatoka.offlinepoker'
    version = '0.1.15'
    status = 'development'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        provided
    }
    sourceSets {
        main { compileClasspath += configurations.provided }
    }

    dependencies {
        compile 'commons-lang:commons-lang:2.+'
        compile 'commons-io:commons-io:2.+'
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile 'com.google.inject:guice:3.0'
        compile 'javax.inject:javax.inject:1'
        compile 'javax.validation:validation-api:1.1.0.Final'
        compile 'org.eclipse.persistence:eclipselink:2.5.2'
        compile 'javax.ejb:ejb-api:3.0'
        compile 'xalan:xalan:2.7.2'

        runtime "ch.qos.logback:logback-classic:$logbackVersion"
		runtime 'org.postgresql:postgresql:9.3-1102-jdbc4'
		compile 'org.apache.commons:commons-dbcp2:2.0.1'
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.apache.derby:derby:$apacheDerbyVersion"
        testCompile 'xmlunit:xmlunit:1.6'

        testRuntime "org.apache.derby:derby:$apacheDerbyVersion"
        testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
    }

    sonarRunner {
        sonarProperties {
            property "sonar.dynamicAnalysis", "reuseReports"
            property "sonar.host.url" , sonarURL
            property "sonar.projectName", "OfflinePoker " + name
            property "sonar.projectKey", "offlinepoker:${name}"
            property "sonar.projectVersion", "0.1"
            property "sonar.java.source", "8"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.jacoco.reportPath", "$buildDir/jacoco/jacocoTest.exec"
        }
    }

    jacocoTestReport {
        reports {
            csv.enabled false
            xml.enabled true
            html.enabled true
            html.destination "${buildDir}/jacoco/html"
        }
    }
    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        }
     }

    jacoco {
        reportsDir = file("$buildDir/jacoco/report")
    }

    jar {
        manifest {
            // the manifest of the default jar is of type OsgiManifest
            name = project.name
            instruction 'Bundle-Description', project.name
            instruction 'Bundle-Vendor', bundleVendor
        }
    }
}

project('de.hatoka.account')
{
    apply plugin: "war"

    sourceSets {
        main {
          output.resourcesDir = "build/classes/main"
        }
    }

    dependencies {
        compile project(":de.hatoka.common")
        compile project(":de.hatoka.address")
        compile project(":de.hatoka.mail")
        compile "org.glassfish.jersey.core:jersey-common:$jerseyVersion"
        compile "org.glassfish.jersey.core:jersey-server:$jerseyVersion"
        compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
        provided "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
        testCompile "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
        runtime "javax.mail:mail:$mailVersion"
    }
}

project('de.hatoka.address')
{
    dependencies {
        compile project(":de.hatoka.common")
    }
}

project('de.hatoka.common')
{
    dependencies {
        compile "org.glassfish.jersey.core:jersey-common:$jerseyVersion"
        provided "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
        testCompile "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
    }
}

project('de.hatoka.mail')
{
    dependencies {
        compile project(":de.hatoka.common")
        compile "javax.mail:mail:$mailVersion"
        provided "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
        testCompile "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
    }
    test {
        // https://javamail.java.net/nonav/docs/api/com/sun/mail/smtp/package-summary.html
        // systemProperty 'SMTP_HOST', mailSmtpHost
        // systemProperty 'SMTP_USER', mailSmtpUser
        // systemProperty 'SMTP_PASSWORD', mailSmtpPassword
        // explicitly include or exclude tests
        exclude 'de/hatoka/mail/internal/service/**'
    }
}

project('de.hatoka.tournament')
{
    apply plugin: "war"

    sourceSets {
        main {
          output.resourcesDir = "build/classes/main"
        }
    }

    dependencies {
        compile project(":de.hatoka.common")
        compile project(":de.hatoka.jndi")
        compile project(":de.hatoka.address")
        compile "org.glassfish.jersey.core:jersey-common:$jerseyVersion"
        compile "org.glassfish.jersey.core:jersey-server:$jerseyVersion"
        compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
        provided "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
        testCompile "org.apache.tomcat:tomcat-catalina:$tomcatVersion"
    }
}
// based on http://www.jroller.com/aalmiray/entry/gradle_glam_jacoco_coveralls
task jacocoReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(fileTree('.').include('**/*.exec').getFiles())
    reports {
        csv.enabled = false
        html.enabled = true
        html.destination "${buildDir}/reports/jacoco/html"
        xml.enabled = true
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

defaultTasks 'test', 'war', 'jacocoReport'
